# Author    : Caleb
# Date      : 11th Feb 2022
# Modified  : 11th Feb 2022
#
# Note		: This is a very bare bones style of repl. It has no memory of the previous code.
#			: Since we append into a main procedure, we cannot create our own procedures. Until
#			: nested procedures are implemented, this simply will not work. So it is limited to
#			: basic expressions and statements.
using 'std:strings'

proc main() -> void {
	true loop {
		'Repl> ' @input

		bind | source |

		# Check if we want to quit
		source 'quit' |!2| !std_str_cmp if {
			@drop_stack
			'quitting' println drop
			false
		} else {
			source 'file' |!2| !std_str_cmp if {
				'File path> ' @input
				bind | fileName |

				# Only present if the file exists
				fileName @file_exists if {
					'Running file: ' print drop fileName println
					@read_file @eval
					'Exiting: ' print drop fileName println
				} else {
					'File does not exist: ' println drop
				}

				true
			} else {
				# We must insert the code into a main block, since we don't currently support
				# top-level code.
				source '}' +
				bind | source |
	
				'proc main() -> void { ' source +
				bind | source |
	
				source
	
				# Evaluate and continue
				@eval
				true
			}
		}
	}
}